buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'org.jetbrains.kotlin.jvm' version '1.7.22'
    id 'application'
}

group 'fi.hsl'
version '1.1.1'

repositories {
    mavenCentral()
    maven {
        url "https://maven.pkg.github.com/HSLdevcom/*"
        credentials {
            username = project.findProperty("github.user") ?: System.getenv("GITHUB_ACTOR")
            password = project.findProperty("github.token") ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

shadowJar {
    archiveFileName ="${baseName}.${extension}"
}

jar {
    manifest {
        attributes(
                'Main-Class': 'fi.hsl.transitdata.eke_sink.MainKt',
                'Implementation-Version': project.version
        )
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation "org.mockito.kotlin:mockito-kotlin:4.1.0"

    testImplementation "org.testcontainers:testcontainers:1.17.6"
    testImplementation "org.testcontainers:junit-jupiter:1.17.6"

    implementation "io.github.microutils:kotlin-logging:1.6.22"

    implementation('fi.hsl:transitdata-common:1.4.4') {
        exclude group: 'javax.xml.bind', module: 'activation'
    }
    //Needed for Pulsar
    implementation 'jaxb:activation:1.0.2'

    implementation "com.azure:azure-storage-blob:12.13.0"
    implementation "io.netty:netty-tcnative-boringssl-static:2.0.39.Final"

    implementation 'org.apache.commons:commons-csv:1.9.0'
    implementation "org.apache.commons:commons-compress:1.21"
    implementation "commons-codec:commons-codec:1.15"

    //implementation  fileTree(dir: 'libs', include: '*.jar')
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_11
}

compileTestKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_11
}

application {
    mainClassName = 'fi.hsl.transitdata.eke_sink.MainKt'
}

